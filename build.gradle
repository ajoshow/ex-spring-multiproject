// https://github.com/unbroken-dome/gradle-testsets-plugin
// https://www.testwithspring.com/lesson/running-integration-tests-with-gradle/
// https://www.petrikainulainen.net/programming/gradle/getting-started-with-gradle-integration-testing-with-the-testsets-plugin/
// https://www.petrikainulainen.net/programming/gradle/getting-started-with-gradle-integration-testing/#integration-test-requirements
// https://docs.gradle.org/current/userguide/java_plugin.html
buildscript {
	ext {
		springBootVersion = '1.5.6.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.4.2'
	}
}

subprojects {
	buildscript {
		repositories {
			mavenCentral()
			jcenter()
		}
		dependencies {
			classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.4.2'
		}
	}

	apply plugin: 'java'
	apply plugin: 'eclipse-wtp'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'war'
	apply plugin: 'idea'
	apply plugin: 'org.unbroken-dome.test-sets'


	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = 1.8

	repositories {
		mavenCentral()
		jcenter()
	}

	configurations {
		providedRuntime
	}

	testSets {
		integrationTest { dirName = 'integration-test' }
		andyTest { dirName = 'andy-test' }
	}

	check.dependsOn integrationTest
	integrationTest.mustRunAfter test

	check.dependsOn andyTest
	andyTest.mustRunAfter test

	dependencies {
		compile('org.springframework.boot:spring-boot-starter-web')
		providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
		testCompile('org.springframework.boot:spring-boot-starter-test')
		integrationTestCompile 'org.assertj:assertj-core:3.0.0'
		andyTestCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
	}

}

task integrationTestOnProjectB(dependsOn : ':ProjectB:integrationTest'){}
task integrationTestOnProjectC(dependsOn : ':ProjectC:integrationTest'){}